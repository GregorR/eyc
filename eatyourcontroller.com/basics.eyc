Copyright (c) 2020, 2021 Gregor Richards;

/*
 * Basic components required for many 2D games
 */

license (
Permission to use, copy, modify, and/or distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND
FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
PERFORMANCE OF THIS SOFTWARE. 
);

alias core.*;

// Objects that move with velocity
export class Motile : Object {
    override mutating this suggestion act() {
        suggestion ret = super();
        this.x += this.vx;
        this.y += this.vy;
        return ret;
    }

    mutating this void accelerateX(num by) {
        this.vx += by;
    }

    mutating this void accelerateY(num by) {
        this.vy += by;
    }

    num vx, vy;
}

// Impassable walls
export class Wall : ObjectSprite, Clonable {
    // Set blocking based on a string
    mutating this void setBlocking(string blocking) {
        this.blocking = new {
            if (blocking == "") {
                // All directions
                this["n"] = this["s"] = this["e"] = this["w"] = true;
            } else {
                for (string b in blocking)
                    this[b] = true;
            }
        };
    }

    // Initialize with full bounding box details
    mutating this void initFull(string sprite, num bbx, num bbw, num bby, num bbh) {
        this.staticSprite = sprite;
        this.bbx1 = bbx;
        this.bbx2 = bbw - bbx;
        this.bby1 = bby;
        this.bby2 = bbh - bby;
    }

    // Initialize with a simple scale and 1 pixel flop
    mutating this void init(string sprite, string blocking, num scale) {
        num x = 0, w = 1, y = 0, h = 1, u = 1/scale;
        this.setBlocking(blocking);
        if (this.blocking["n"]) {
            y = u;
            h -= u;
        }
        if (this.blocking["e"])
            w -= u;
        if (this.blocking["s"])
            h -= u;
        if (this.blocking["w"]) {
            x = u;
            w -= u;
        }
        this.initFull(sprite, x, w, y, h);
    }

    // Clone from another wall
    override mutating this void clone(Clonable from) {
        super(from);
        Wall other = from : Wall;
        this.cloneSprite(other);
        this.blocking = clone other.blocking;
    }

    override string preinteract(Interaction interaction) {
        super(interaction);
        return "solid";
    }

    override suggestion interact(array(tuple(Interaction, Interaction)) interactions) {
        suggestion ret = super(interactions);
        for (tuple(Interaction, Interaction) ip in interactions) {
            Interaction i = ip[0];
            Motile other = i.from : Motile;
            string dirs = i.dir(0x.4);

            // Bump them in the appropriate direction(s)
            for (string dir in dirs) {
                if (this.blocking[dir]) {
                    if (dir == "n") {
                        num over = this.y + this.bby1 - other.y - other.bby2;
                        ret = ret suggest {
                            other.moveY(-over);
                            other.accelerateY(-other.vy);
                        };

                    } else if (dir == "e") {
                        num over = other.x + other.bbx1 - this.x - this.bbx2;
                        ret = ret suggest {
                            other.moveX(over);
                            other.accelerateX(-other.vx);
                        };

                    } else if (dir == "s") {
                        num over = other.y + other.bby1 - this.y - this.bby2;
                        ret = ret suggest {
                            other.moveY(over);
                            other.accelerateY(-other.vy);
                        };

                    } else if (dir == "w") {
                        num over = this.x + this.bbx1 - other.x - other.bbx2;
                        ret = ret suggest {
                            other.moveX(-over);
                            other.accelerateX(-other.vx);
                        };

                    }
                }
            }
        }
        return ret;
    }

    // Directions that this wall is meant to block
    set(string) blocking;
}

/* A simple data type indicating damage that an object should in some way react
 * to */
export class Damage {
    mutating this void init(Object from, string type, num amount) {
        this.from = from;
        this.type = type;
        this.amount = amount;
    }

    Object from;
    string type;
    num amount;
}

// A damageable object
export class Damageable : Object {
    array(Damage) damage;

    // Call this to indicate that the object should receive damage
    mutating void takeDamage(Damage damage) {
        this.damage += damage;
    }

    // Override this to handle the damage received in this frame
    mutating this suggestion handleDamage(array(Damage) damage) {}

    override mutating this suggestion react() {
        suggestion ret = super() + this.handleDamage(this.damage);
        this.damage = new;
        return ret;
    }
}
